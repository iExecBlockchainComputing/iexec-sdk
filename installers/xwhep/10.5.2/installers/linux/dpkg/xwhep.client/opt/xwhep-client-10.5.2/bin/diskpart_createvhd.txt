
REM Copyrights     : CNRS
REM Author         : Oleg Lodygensky
REM Acknowledgment : XtremWeb-HEP is based on XtremWeb 1.8.0 by inria : http://www.xtremweb.net/
REM Web            : http://www.xtremweb-hep.org
REM 
REM      This file is part of XtremWeb-HEP.
REM
REM    XtremWeb-HEP is free software: you can redistribute it and/or modify
REM    it under the terms of the GNU General Public License as published by
REM    the Free Software Foundation, either version 3 of the License, or
REM    (at your option) any later version.
REM
REM    XtremWeb-HEP is distributed in the hope that it will be useful,
REM    but WITHOUT ANY WARRANTY; without even the implied warranty of
REM    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
REM    GNU General Public License for more details.
REM
REM    You should have received a copy of the GNU General Public License
REM    along with XtremWeb-HEP.  If not, see <http://www.gnu.org/licenses/>.
REM
REM



REM
REM File    : diskpart_createvhd.txt
REM OS      : Windows 7
REM Purpose : This file contains diskpart commands to create and mount a new Virtual Hard Drive (VHD)
REM Usage   :
REM          -1- open a new terminal (cmd.exe)
REM          -2- call diskpart
REM          -3- copy and paste the following commands
REM

REM 
REM This create a new file
REM The file must not exist, or this fails
REM
REM If you want to reuse an existing file, please use:
REM        SELECT  VDISK FILE="%TMP%\xwcontext.vhd"
REM

CREATE VDISK FILE="%TMP%\xwcontext.vhd" MAXIMUM="40" TYPE="EXPANDABLE"


REM 
REM This attaches the VHD
REM 

ATTACH VDISK


REM 
REM This creates a new primary partition in VHD
REM 

CREATE PARTITION PRIMARY


REM 
REM This assings a drive letter to VHD
REM This fails if letter is already in use
REM

ASSIGN LETTER=E


REM 
REM This formats VHD in FAT32
REM 

FORMAT QUICK FS="FAT32" LABEL="XWCONTEXT"


REM
REM ********************************************************
REM *                                                      *
REM * From this point, you can copy your files to your VHD *
REM *                                                      *
REM ********************************************************
REM




REM 
REM This detaches the VHD
REM 

DETACH VDISK


